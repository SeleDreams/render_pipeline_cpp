cmake_minimum_required(VERSION 3.12)

@PACKAGE_INIT@

include(CMakeFindDependencyMacro)
find_dependency(panda3d REQUIRED p3framework p3direct)
find_dependency(Boost REQUIRED)

if(CMAKE_SYSTEM_NAME STREQUAL Android)
    include(${CMAKE_CURRENT_LIST_DIR}/@render_pipeline_TARGET_EXPORT_NAME@-${ANDROID_ABI}.cmake)
else()
    include(${CMAKE_CURRENT_LIST_DIR}/@render_pipeline_TARGET_EXPORT_NAME@.cmake)
endif()
include(${CMAKE_CURRENT_LIST_DIR}/@PACKAGE_NAME@-macro.cmake)

set(@PACKAGE_NAME@_VERSION "@PACKAGE_VERSION@")
set(@PACKAGE_NAME@_INSTALL_DIR ${PACKAGE_PREFIX_DIR})

get_target_property(render_pipeline_IMPORTED_CONFIGURATIONS render_pipeline::render_pipeline IMPORTED_CONFIGURATIONS)
if(NOT RELWITHDEBINFO IN_LIST render_pipeline_IMPORTED_CONFIGURATIONS)
    set_target_properties(render_pipeline::render_pipeline PROPERTIES
        MAP_IMPORTED_CONFIG_RELWITHDEBINFO RELEASE
    )
endif()

if(NOT MINSIZEREL IN_LIST render_pipeline_IMPORTED_CONFIGURATIONS)
    set_target_properties(render_pipeline::render_pipeline PROPERTIES
        MAP_IMPORTED_CONFIG_MINSIZEREL RELEASE
    )
endif()

message(STATUS "@PACKAGE_NAME@ version: ${@PACKAGE_NAME@_VERSION}")

check_required_components(@PACKAGE_NAME@)
